$ ->
  initialize = ->
    initializeConfirmationDialog()
    initializeExtendedFilter()

  initializeConfirmationDialog = ->
    $('#dialog-confirm').dialog
      autoOpen: false
      resizable: false
      height: 'auto'
      width: 'auto'
      modal: true
      title: '<%= I18n.t('submit_changes') %>'
      buttons:
        'confirm':
          text: '<%= I18n.t(true) %>'
          click: ->
            form = $('.edit_issue')
            tabData = $(@).data('url').split('?')[1].split('=')
            form.append($('<input>').attr('type', 'hidden').attr('name', tabData[0]).val(tabData[1]))
            Rails.fire(form[0], 'submit')
            $(@).dialog('close')
          class: 'btn btn-sm btn-primary'
        'reject':
          text: '<%= I18n.t(false) %>'
          click: ->
            $.ajax(url: $(@).data('url'), type: 'GET')
            $(@).dialog('close')
          class: 'btn btn-sm btn-secondary'
        'abort':
          text: '<%= I18n.t('action.abort') %>'
          click: ->
            $(@).dialog('close')
          class: 'btn btn-sm btn-secondary'

  initializeExtendedFilter = ->
    if $('form#issues-filter').length == 1
      KS.initializeFormActions()

  $(document).on 'turbolinks:load', initialize

  $(document).on 'change', '#status_note_template', ->
    $('#issue_status_note').val($('#status_note_template')[0]['value'])
    $('#status_note_template').val('')

  $(document).on 'click', '#issue_email_direct', (e) ->
    e.preventDefault()
    $.ajax
      url: $('#issue_email_direct').attr('href')
      type: 'GET'
      success: (data) ->
        location.href = data

  $(document).on 'change', '.edit_issue :input:not([name="map_view"])', ->
    $('.edit_issue').data('changed', true)
  
  $(document).on 'click', '.switch-tab', (e) ->
    e.preventDefault()
    return $.ajax(url: $(@).data('url'), type: 'GET') if !$('.edit_issue').data('changed')
    $('#dialog-confirm').data('url', $(@).data('url'))
    $('#dialog-confirm').dialog('open')

  $(document).on 'click', '#issue_export', (e) ->
    reopen = false
    if $('#map').size() == 0
      $('#print-map').attr('id', 'map')
    else
      reopen = true
    $('#map').html('')
    KS.initializeMaps(false)
    e.preventDefault()
    href = $('#issue_export').attr('href')

    window.setTimeout (->
      canvas = document.getElementsByTagName('canvas').item(0)
      img = canvas.toDataURL('image/png')

      req = new XMLHttpRequest
      req.open 'POST', $('#issue_export').attr('href'), true
      req.responseType = 'blob'
      req.setRequestHeader "Content-Type", "application/x-www-form-urlencoded; charset=UTF-8"
      req.send 'map_image=' + btoa(img)

      req.onload = (event) ->
        blob = req.response
        link = document.createElement('a')
        link.href = window.URL.createObjectURL(blob)
        link.download = req.getResponseHeader('X-FileName')
        link.click()

      $('#map').html('')
      KS.initializeMaps(true)
    ), 1000

  $(document).on 'click', '.card .btn-create', (e) ->
    e.preventDefault()
    link = $(e.target).parents('.card').find('.btn-create')
    $.ajax
      url: link.attr('href')
      type: 'POST'
      data: link.parents('.card').find(':input')

  $(document).on 'click', '.card .btn-update', (e) ->
    e.preventDefault()
    link = $(e.target).parents('.card').find('.btn-update')
    console.log($(e.target), link)
    $.ajax
      url: link.attr('href')
      type: 'PUT'
      data: link.parents('.card').find(':input')

  $(document).on 'click', '#dms_link', (e) ->
    e.preventDefault()
    open_url = ''
    $.ajax
      url: '<%= Rails.application.routes.url_helpers.dms_index_path %>'
      type: 'GET'
      data: { issue_id: $(e.target).data('id') }
      async: false
      success: (data) ->
        open_url = data
    window.open(open_url, '_blank')

  $(document).on 'focus', '#comment_message', (e) ->
    $('input[type="submit"][name="commit"]').addClass('disabled')
    $('input[type="submit"][name="save_and_close"]').addClass('disabled')

  $(document).on 'focusout', '#comment_message', (e) ->
    $('input[type="submit"][name="commit"]').removeClass('disabled')
    $('input[type="submit"][name="save_and_close"]').removeClass('disabled')

  $(document).on 'click', '#rejection-link', (e) ->
    e.preventDefault()
    $('#completion-rejection').toggleClass('d-none')
